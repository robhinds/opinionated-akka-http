buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "gradle.plugin.com.github.maiflai:gradle-scalatest:0.22"
    }
}

subprojects {
    group 'io.github.robhinds.akk-ops'
    version '0.0.1'

    apply plugin: 'scala'
    apply plugin: "com.github.maiflai.scalatest"
    apply plugin: "com.jfrog.bintray"
    apply plugin: "maven-publish"

    sourceCompatibility = 1.8

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    javadoc.failOnError = false
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    repositories {
        mavenCentral()
    }

    ext {
        scalaSuffix = "2.12"
        scalaVersion = "$scalaSuffix.4"
        catsVersion = "0.9.0"
        scalaTestVersion = "3.0.4"
        akkaHttpVersion = "10.0.10"
        akkaActorVersion = "2.4.19"
        circeVersion = "0.8.0"
        akkaHttpCirceVersion = "1.20.1"
        akkaHttpSessionVersion = "0.5.5"
        loggingVersion = "3.9.0"
        pegDownVersion = "1.4.2"
        junitVersion = "4.12"
    }

    dependencies {
        compile "org.scala-lang:scala-library:$scalaVersion"
        compile "org.typelevel:cats-core_$scalaSuffix:$catsVersion"
        compile "com.typesafe.scala-logging:scala-logging_$scalaSuffix:$loggingVersion"

        testCompile "org.scalactic:scalactic_$scalaSuffix:$scalaTestVersion"
        testCompile "org.scalatest:scalatest_$scalaSuffix:$scalaTestVersion"
        testRuntime "org.pegdown:pegdown:$pegDownVersion"
        testCompile "junit:junit:$junitVersion"
    }

}
